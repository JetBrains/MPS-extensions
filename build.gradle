//will pull the groovy classes/types from nexus to the classpath
buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://artifacts.itemis.cloud/repository/maven-mps/' }
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    }
    dependencies {
        classpath('de.itemis.mps:mps-gradle-plugin:1.7+')
        //classpath("org.modelix.mpsbuild:gradle-plugin:1.0.+")
    }
}

plugins {
    id "co.riiid.gradle" version "0.4.2"
}

apply plugin: 'maven-publish'
apply plugin: 'base'



import de.itemis.mps.gradle.*

import java.time.LocalDateTime


wrapper {
    gradleVersion '7.4.1'
    distributionType 'all'
}

// detect if we are in a CI build
if (project.hasProperty("forceCI")) {
    ext.ciBuild = true
} else {
    //on teamcity we are in a CI build
    if (project.hasProperty("teamcity") && !project.hasProperty('mpsHomeDir')) {
        ext.ciBuild = true
    } else {
        ext.ciBuild = false
    }
}

def jdk_home

if (ext.has('java11_home')) {
    jdk_home = ext.get('java11_home')
} else if (System.getenv('JB_JAVA11_HOME') != null) {
    jdk_home = System.getenv('JB_JAVA11_HOME')
} else {
    def expected = JavaVersion.VERSION_11
    if (JavaVersion.current() != expected) {
        throw new GradleException("This build script requires Java 11 but you are currently using ${JavaVersion.current()}.\nWhat you can do:\n"
            + "  * Use project property java11_home to point to the Java 11 JDK.\n"
            + "  * Use environment variable JAVA11_HOME to point to the Java 11 JDK\n"
            + "  * Run Gradle using Java 11")
    }
    jdk_home = System.getProperty('java.home')
}

// Check JDK location
if (!new File(jdk_home, "lib").exists()) {
    throw new GradleException("Unable to locate JDK home folder. Detected folder is: $jdk_home")
}

ext.jdk_home = jdk_home
logger.info 'Using JDK at {}', jdk_home

// Default repository credentials
if (!project.hasProperty('nexusUsername')) {
    ext.nexusUsername = ''
    ext.nexusPassword = ''
}
logger.info 'Repository username: {}', project.nexusUsername

ext.dependencyRepositories = [
        'https://artifacts.itemis.cloud/repository/maven-mps/',
        'https://projects.itemis.de/nexus/content/repositories/mbeddr',
        'https://projects.itemis.de/nexus/content/repositories/mbeddr_snapshots',
]
// Dependency versions

// major version, e.g. '2021.1', '2021.2'
ext.mpsMajor = '2021.2'
// optional minor/bugfix number (not added to the final build version)
ext.mpsMinor = '6'
// e.g. Beta, EAP, RC
ext.mpsReleaseType = ''

def appendOpt = { str,pre -> !str.isEmpty() ? "${pre}${str}" : "" }
ext.mpsVersion =  "$mpsMajor" + appendOpt(mpsMinor, '.') + appendOpt(mpsReleaseType, '-')

if (ciBuild) {
    String branch = GitBasedVersioning.gitBranch

    def buildMajor = mpsMajor.split("\\.").first()
    def buildMinor = mpsMajor.split("\\.").last()
    def buildNumber = System.env.BUILD_NUMBER.toInteger()
    if (branch.matches(/(maintenance|mps)[\/-](mps)?\d+(\.\d+)*/)) {
        version = "$buildMajor.$buildMinor.$buildNumber.${GitBasedVersioning.gitShortCommitHash}"
    } else {
        version = GitBasedVersioning.getVersionWithCount(buildMajor, buildMinor, buildNumber)
    }

    println "##teamcity[buildNumber '${version}']"
} else {
    println "Local build detected, version will be SNAPSHOT"
    version = "1.0-SNAPSHOT"
}

def userHome = System.properties['user.home']
def mpsPluginsDirPattern
if (System.properties['os.name'].toLowerCase().contains('mac')) {
    mpsPluginsDirPattern = "$userHome/Library/Application Support/%s"
} else {
    mpsPluginsDirPattern = "$userHome/.%s/config/plugins"
}

if (project.hasProperty("MPS_PATHS_SELECTOR")) {
    ext.mpsPluginsDir = sprintf(mpsPluginsDirPattern, project.getProperty("MPS_PATHS_SELECTOR"))
} else {
    ext.mpsPluginsDir = sprintf(mpsPluginsDirPattern, "MPS$mpsMajor")
}

ext.releaseRepository = 'https://artifacts.itemis.cloud/repository/maven-mps-releases/'
ext.snapshotRepository = 'https://artifacts.itemis.cloud/repository/maven-mps-snapshots/'
ext.publishingRepository = version.toString().endsWith("-SNAPSHOT") ? snapshotRepository : releaseRepository


configurations {
    mps
}

dependencies {
    mps "com.jetbrains:mps:$mpsVersion"
}

repositories {
    mavenLocal()
    for (repoUrl in project.dependencyRepositories) {
        maven {
            url repoUrl
        }
    }
}

ext.skipResolveMps = project.hasProperty('mpsHomeDir')
ext.mpsHomeDir = rootProject.file(project.findProperty('mpsHomeDir') ?: "$buildDir/mps")

if (project.skipResolveMps) {
    task resolveMps {
        doLast {
            logger.info "MPS resolution skipped"
            logger.info "MPS home: " + mpsHomeDir.getAbsolutePath()
        }
    }
} else {
    task resolveMps(type: Copy) {
        dependsOn configurations.mps
        from {
            configurations.mps.resolve().collect { zipTree(it) }
        }
        into mpsHomeDir
    }
}

// -------- Model API ----------------------

configurations {
    modelApi
}

dependencies {
    def modelixCoreVersion = "1.5.4"
    modelApi("org.modelix:model-api:$modelixCoreVersion")
    modelApi("org.modelix:metamodel-generator:$modelixCoreVersion")
}

task copyModelApi() {
    dependsOn configurations.modelApi
    doLast {
        // copy transitive dependencies without version in the file name
        // otherwise each new version would require a change of the MPS solution
        def libFolder = file("$projectDir/code/model-api/org.modelix.model.api/lib")
        libFolder.deleteDir()
        libFolder.mkdir()
        def versionsFile = file("$libFolder/versions.txt")
        for (final def artifact in configurations.modelApi.resolvedConfiguration.resolvedArtifacts) {
            // these libs are part of MPS
            if (
                    artifact.moduleVersion.id.name.startsWith("log4j")
                    || artifact.moduleVersion.id.name.startsWith("annotations")
                    || artifact.moduleVersion.id.name.startsWith("slf4j-api")

            ) {
                versionsFile.append("already part of mps: " + artifact.file.name + "\n")
                continue
            }

            def sourceFile = java.nio.file.Path.of(artifact.file.absolutePath)
            def targetFile = java.nio.file.Path.of(libFolder.absolutePath).resolve(artifact.moduleVersion.id.name + "." + artifact.extension)
            java.nio.file.Files.copy(sourceFile, targetFile)
            versionsFile.append(artifact.file.name + "\n")
        }
    }
}

// -----------------------------------------

// Ant support
configurations {
    ant_lib
}

dependencies {
    ant_lib "org.apache.ant:ant-junit:1.10.9"
}

ext.buildScriptClasspath = project.configurations.ant_lib.fileCollection({
    true
})

def artifactsDir = new File(rootDir, 'artifacts')


ext.mps_home = '-Dmps.home=' + mpsHomeDir.getAbsolutePath()
ext.build_dir = '-Dbuild.dir=' + file(rootProject.projectDir.absolutePath).getAbsolutePath()
ext.artifacts_dir = '-Dartifacts.root=' + artifactsDir
ext.pluginVersion = "-DversionNumber=" + version
ext.buildDate = "-DbuildDate=" + new Date().toString()
ext.extensions_home = '-Dextensions.home=' + rootDir
ext.skipUnmodifiedModels = "-Dmps.generator.skipUnmodifiedModels=true" //incremental build support

// ___________________ utilities ___________________
File scriptFile(String relativePath) {
    new File("$rootDir/build/generated/$relativePath")
}

def defaultScriptArgs = [mps_home, build_dir, artifacts_dir, ext.buildDate, ext.pluginVersion, ext.skipUnmodifiedModels]

// enables https://github.com/mbeddr/mps-gradle-plugin#providing-global-defaults
ext["itemis.mps.gradle.ant.defaultScriptArgs"] = defaultScriptArgs
ext["itemis.mps.gradle.ant.defaultScriptClasspath"] = buildScriptClasspath
ext["itemis.mps.gradle.ant.defaultJavaExecutable"] = new File(jdk_home, 'bin/java')

tasks.register("checkModulePaths") {
    def mps_dot_folder = ".mps";
    def modules_xml_location = "modules.xml"
    def file = "$projectDir/code/$mps_dot_folder/$modules_xml_location"
    def project = new XmlParser().parse(file)

    def invalidModules = project.component.projectModules.modulePath.'@path'.findAll {
        def path = it.replaceAll("\\\$PROJECT_DIR\\\$","$rootDir/code")
        return !(new File(path).exists())
    }

    if(!invalidModules.isEmpty()) {
        throw new GradleException("The following modules in $modules_xml_location don't have valid paths:\n ${invalidModules.toArray().join('\n')}")
    }
}

check.configure {
    dependsOn checkModulePaths
}

task build_allScripts(type: BuildLanguages, dependsOn: [
        resolveMps,
        copyModelApi,
]) {
    script "$rootDir/scripts/build.xml"
}

task build_languages(type: BuildLanguages, dependsOn: [build_allScripts]) {
    script scriptFile('languages/build.xml')
}

task run_tests(type: TestLanguages, dependsOn: build_languages) {
    description "Will execute all tests from command line"
    script scriptFile('tests/build.xml')
}

task install_nativelibs(type: Copy, dependsOn: build_languages) {
    from "$rootDir/artifacts/de.itemis.mps.extensions/"
    include "de.itemis.mps.nativelibs.loader/"
    into "$mpsPluginsDir"
}


task install(dependsOn: install_nativelibs) {
    description "Install the required plugins into the MPS plugin repository"
    group "Build Setup"
    doFirst {
        // check parent gradle file for definition of the variables
        println "Installing required mbeddr plugins to '$mpsPluginsDir'"
        if (!project.hasProperty("MPS_PATHS_SELECTOR")) {
            println "To change 'MPS<>' part, pass MPS_PATHS_SELECTOR property to gradle with -PMPS_PATHS_SELECTOR=<custom path selector>"
            println "The path selector only contains the the actual selector for instance \"MPS2017.3\" not the full qualifies path to the user plugin directory."
        }
    }
}

//

// Ant <junit> task support
repositories {
    mavenCentral()
}
configurations {
    junitAnt
}
dependencies {
    junitAnt 'junit:junit:4.12'
    junitAnt('org.apache.ant:ant-junit:1.10.9') {
        transitive = false
    }
    junitAnt('org.apache.ant:ant-junit4:1.10.9') {
        transitive = false
    }
}

task packageExtensions(type: Zip, dependsOn: run_tests) {
    baseName 'de.itemis.mps.extensions'
    from artifactsDir
    include 'de.itemis.mps.extensions/**'
}

task cleanMps(type: Delete) {
    delete fileTree(dir: projectDir, include: ["**/classes_gen/**", "**/source_gen/**", "**/source_gen.caches/**", "tmp/**", "artifacts/**"])
}

clean.dependsOn cleanMps
assemble.dependsOn packageExtensions

allprojects {
    apply plugin: 'maven-publish'
    publishing {
        repositories {
            if (rootProject.hasProperty("artifacts.itemis.cloud.user") && rootProject.hasProperty("artifacts.itemis.cloud.pw")) {
                maven {
                    name = "itemisCloud"
                    url = project.publishingRepository
                    credentials {
                        username = rootProject.findProperty("artifacts.itemis.cloud.user")
                        password = rootProject.findProperty("artifacts.itemis.cloud.pw")
                    }
                }
            }
            if (rootProject.hasProperty("gpr.token")) {
                maven {
                    name = "GitHubPackages"
                    url = "https://maven.pkg.github.com/JetBrains/MPS-extensions"
                    credentials {
                        username = rootProject.findProperty("gpr.user")
                        password = rootProject.findProperty("gpr.token")
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        extensions(MavenPublication) {
            groupId 'de.itemis.mps'
            artifactId 'extensions'
            artifact packageExtensions
            //Pom.withProvidedDep pom, configurations.mps
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.mps.resolvedConfiguration.firstLevelModuleDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleGroup)
                    dependencyNode.appendNode('artifactId', it.moduleName)
                    dependencyNode.appendNode('version', it.moduleVersion)
                    dependencyNode.appendNode('type', it.moduleArtifacts[0].type)
                    dependencyNode.appendNode('scope', 'provided')
                }
            }
        }
    }
}

task pipInstall(type: Exec) {
    inputs.file("requirements.txt")
    commandLine "pip", "install", "-r", "requirements.txt"
}

task previewDocs(type: Exec, dependsOn: pipInstall) {
    commandLine "mkdocs", "serve"
}

task deployDocs(type: Exec, dependsOn: pipInstall) {
    commandLine "mkdocs", "gh-deploy", "--clean", "-r", "gh-pages", "--force"
}

defaultTasks 'build_languages'
task test(dependsOn: run_tests)

build_languages.mustRunAfter clean

task rebuild(dependsOn:[clean, build_languages])

def releaseNotes
def releaseName
def releaseTagName

if (rootProject.hasProperty("nightly_build")) {
    releaseName = 'Nightly Build ' + version
    releaseTagName = 'nightly-' + version
    releaseNotes = """Automated Nightly build from ${LocalDateTime.now() as String}."""
} else {
    releaseNotes = rootProject.findProperty("releaseNotes")
    releaseTagName = 'release-' + version
    releaseName = version
}


github {
    owner = 'jetbrains'
    repo = 'MPS-extensions'
    token = rootProject.findProperty("github.token") ?: "empty"
    tagName = releaseTagName
    targetCommitish = GitBasedVersioning.getGitCommitHash()
    name = releaseName
    body = releaseNotes
    prerelease = rootProject.hasProperty("nightly_build")
    assets = packageExtensions.outputs.files.files.collect {it.path}
}

githubRelease.dependsOn packageExtensions

/*
group "io.github.jetbrains.mps-extensions"
apply plugin: "modelix-gradle-mpsbuild-plugin"

mpsBuild {
    dependsOn(copyModelApi)

    mpsVersion("$mpsVersion")
    stubs("org.apache.commons:commons-math3:3.6.1")
    search("code")

    publication("shadowmodels") {
        module("de.q60.mps.polymorphicfunctions")
        module("de.q60.mps.polymorphicfunctions.runtime")
        module("de.q60.mps.shadowmodels.debugview")
        module("de.q60.mps.shadowmodels.devkit")
//        module("de.q60.mps.shadowmodels.examples.blext")
//        module("de.q60.mps.shadowmodels.examples.entities")
//        module("de.q60.mps.shadowmodels.examples.input")
//        module("de.q60.mps.shadowmodels.examples.statemachine")
        module("de.q60.mps.shadowmodels.gen.afterPF")
        module("de.q60.mps.shadowmodels.gen.desugar")
        module("de.q60.mps.shadowmodels.gen.typesystem")
        module("de.q60.mps.shadowmodels.genplan")
        module("de.q60.mps.shadowmodels.modelcheck.runtime")
        module("de.q60.mps.shadowmodels.repository")
        module("de.q60.mps.shadowmodels.runtime")
        module("de.q60.mps.shadowmodels.runtimelang")
        module("de.q60.mps.shadowmodels.target.editor")
        module("de.q60.mps.shadowmodels.target.text")
        module("de.q60.mps.shadowmodels.transformation")
        module("de.q60.mps.shadowmodels.util")
        module("de.q60.mps.explorer")
        module("de.q60.mps.explorer.impl")
        module("de.q60.mps.incremental")
        module("de.q60.mps.incremental.runtime")
    }

    publication("diagram") {
        module("de.itemis.mps.editor.diagram")
        module("de.itemis.mps.editor.diagram.devkit")
        module("de.itemis.mps.editor.diagram.layout")
        module("de.itemis.mps.editor.diagram.runtime")
        module("de.itemis.mps.editor.diagram.shapes")
        module("de.itemis.mps.editor.diagram.styles")
    }

    publication("grammarcells") {
        module("com.mbeddr.mpsutil.grammarcells")
        module("com.mbeddr.mpsutil.grammarcells.runtime")
        module("com.mbeddr.mpsutil.grammarcells.runtimelang")
    }

    publication("projectview") {
        module("com.mbeddr.mpsutil.projectview")
        module("com.mbeddr.mpsutil.projectview.runtime")
        module("com.mbeddr.mpsutil.projectview.vcs")
        module("com.mbeddr.mpsutil.projectview.views")
    }

    publication("treenotation") {
        module("com.mbeddr.mpsutil.treenotation")
        module("com.mbeddr.mpsutil.treenotation.runtime")
        module("com.mbeddr.mpsutil.treenotation.styles")
    }

    publication("math") {
        module("de.itemis.mps.editor.math")
        module("de.itemis.mps.editor.math.notations")
        module("de.itemis.mps.editor.math.runtime")
        module("de.itemis.mps.editor.math.symbols")
    }

    publication("blutil") {
        module("com.mbeddr.mpsutil.blutil")
        module("com.mbeddr.mpsutil.blutil.genutil")
        module("com.mbeddr.mpsutil.blutil.genutil.rt")
        module("com.mbeddr.mpsutil.blutil.rt")
    }

    publication("plaintextgen") {
        module("com.dslfoundry.plaintextflow")
        module("com.dslfoundry.plaintextgen")
    }

    publication("deprecated") {
        module("de.itemis.mps.modelmerger")
        module("de.itemis.mps.modelmerger.runtime")
    }

    publication("libraries") {
        //module("com.fasterxml.jackson")
        module("org.apache.commons")
    }

    publication("modellisteners") {
        module("com.mbeddr.mpsutil.modellisteners")
        module("com.mbeddr.mpsutil.modellisteners.runtime")
    }

    publication("other") {
        module("com.dslfoundry.langvis.plugin")
        module("com.mbeddr.mpsutil.intentions")
        module("com.mbeddr.mpsutil.intentions.runtime")
        module("com.mbeddr.mpsutil.serializer.xml")
//        module("de.itemis.mps.extensions.build")
        module("de.itemis.mps.nativelibs")
        module("de.itemis.mps.nodeversioning")
        module("de.itemis.mps.nodeversioning.runtime")
        module("de.itemis.mps.selection.intentions")
        module("de.itemis.mps.selection.runtime")
        module("de.q60.mps.collections.libs")
        module("de.q60.mps.logging.runtime")
        module("de.q60.mps.util")
//        module("de.q60.mps.virtualinterfaces")
//        module("de.q60.mps.virtualinterfaces.devkit")
//        module("de.q60.mps.virtualinterfaces.genplan")
    }

    publication("mouselistener") {
        module("de.itemis.mps.mouselistener.runtime")
    }

    publication("modelmerge") {
        module("de.itemis.model.merge")
        module("de.itemis.model.merge.baselang")
        module("de.itemis.model.merge.runtime")
    }

    publication("cellfactory") {
        module("de.slisson.mps.richtext.customcell")
    }

    publication("other-editors") {
        module("de.itemis.mps.editor.dropdown")
        module("de.itemis.mps.editor.dropdown.runtime")
        module("de.itemis.mps.editor.enumeration")
        module("de.itemis.mps.editor.enumeration.runtime")
        module("de.itemis.mps.editor.bool")
        module("de.itemis.mps.editor.bool.runtime")
        module("com.mbeddr.mpsutil.editor.querylist")
        module("com.mbeddr.mpsutil.editor.querylist.runtime")
    }

    publication("collapsible") {
        module("de.itemis.mps.editor.collapsible")
        module("de.itemis.mps.editor.collapsible.runtime")
    }

    publication("celllayout") {
        module("de.itemis.mps.editor.celllayout")
        module("de.itemis.mps.editor.celllayout.runtime")
        module("de.itemis.mps.editor.celllayout.styles")
    }

    publication("hacks") {
        module("de.slisson.mps.hacks.editor")
        module("de.slisson.mps.hacks.xmodelgen")
        module("de.slisson.mps.reflection")
        module("de.slisson.mps.reflection.runtime")
    }

    publication("conditional-editor") {
        module("de.slisson.mps.conditionalEditor")
        module("de.slisson.mps.conditionalEditor.hints")
        module("de.slisson.mps.conditionalEditor.runtime")
    }

    publication("richtext") {
        module("de.slisson.mps.richtext")
        module("de.slisson.mps.editor.multiline")
        module("de.slisson.mps.editor.multiline.runtime")
    }

    publication("tables") {
        module("de.slisson.mps.tables")
        module("de.slisson.mps.tables.runtime")
    }

    publication("model-api") {
        module("org.modelix.model.api")
        module("org.modelix.model.mpsadapters")
        module("org.modelix.model.repositoryconcepts")
    }
}
*/